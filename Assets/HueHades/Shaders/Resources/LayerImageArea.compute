
#pragma kernel NormalBlend;
#pragma kernel MultiplyBlend
#pragma kernel AddBlend
#pragma kernel SubtractBlend;
#pragma kernel EraseKernel;

Texture2D<float4> BottomLayer;
Texture2D<float4> TopLayer;
RWTexture2D<float4> Result;

uint4 SrcDstDim; //Source and Destination texture dimensions
uint2 DstXY; //Destination target offset
uint4 SrcRect; //Source target offset and dimensions
uint4 TileSrcXYDstXY; //Source and Destination tile modes
float Opacity; //opacity of the top layer to apply

bool SelectAndTile(uint3 id, out uint2 targetID, out uint2 sourceID){
    uint2 srcID = SrcRect.xy + id.xy;
    uint2 dstID = DstXY + id.xy;
    
    if(all(
            ((srcID < SrcDstDim.xy) || TileSrcXYDstXY.xy) &&
            ((dstID < SrcDstDim.zw) || TileSrcXYDstXY.zw) &&
            id.xy < SrcRect.zw
       ))
       {
            targetID = dstID % SrcDstDim.zw;
            sourceID = srcID % SrcDstDim.xy;
            return true;
       }
       targetID = 0;
       sourceID = 0;
    return false;
}


[numthreads(8,8,1)]
void EraseKernel (uint3 id : SV_DispatchThreadID)
{
    uint2 targetID;
    uint2 sourceID;
    if(SelectAndTile(id, targetID, sourceID)){
        float4 top = TopLayer[sourceID];
        float4 bottom = BottomLayer[sourceID];

        Result[targetID] = float4(bottom.rgb, saturate(bottom.a - lerp(0,top.a,Opacity)));
    }
}



[numthreads(8,8,1)]
void NormalBlend (uint3 id : SV_DispatchThreadID)
{
    uint2 targetID;
    uint2 sourceID;
    if(SelectAndTile(id, targetID, sourceID)){
        float4 top = TopLayer[sourceID];
        float4 bottom = BottomLayer[sourceID];

        top.a *= Opacity;

        float alphaRatioBottom = (min(1.0 - top.a, bottom.a / (bottom.a + top.a)));
        float alphaRatioTop = 1.0 - alphaRatioBottom;

        Result[targetID] = float4(bottom.rgb * alphaRatioBottom, bottom.a * (1.0 - top.a)) + float4(top.rgb * alphaRatioTop, top.a);
    }
}

[numthreads(8,8,1)]
void MultiplyBlend (uint3 id : SV_DispatchThreadID)
{
    uint2 targetID;
    uint2 sourceID;
    if(SelectAndTile(id, targetID, sourceID)){
        float4 top = TopLayer[sourceID];
        float4 bottom = BottomLayer[sourceID];

        top.a *= Opacity;

        float4 bottomProp = bottom * (1.0 - top.a);
        Result[targetID] = float4(bottom.rgb * lerp(1, top.rgb, top.a),min(1,bottomProp.a + top.a));
    }
}

[numthreads(8,8,1)]
void AddBlend (uint3 id : SV_DispatchThreadID)
{
    uint2 targetID;
    uint2 sourceID;
    if(SelectAndTile(id, targetID, sourceID)){
        float4 top = TopLayer[sourceID];
        float4 bottom = BottomLayer[sourceID];

        top.a *= Opacity;

        float4 bottomProp = bottom * (1.0 - top.a);
        float4 topProp = top * top.a;
        Result[targetID] = min(1.0,float4(bottom.rgb + top.rgb,bottomProp.a + top.a));
    }
}

    [numthreads(8,8,1)]
void SubtractBlend (uint3 id : SV_DispatchThreadID)
{
    uint2 targetID;
    uint2 sourceID;
    if(SelectAndTile(id, targetID, sourceID)){
        float4 top = TopLayer[sourceID];
        float4 bottom = BottomLayer[sourceID];

        top.a *= Opacity;

        float4 bottomProp = bottom * (1.0 - top.a);
        float4 topProp = top * top.a;
        Result[targetID] = max(0.0,float4(bottom.rgb - top.rgb,bottomProp.a + top.a));
    }
}